%{
#include "tree.h"
#include<stdio.h>
#include<iostream>
#include<cstring>
#include<map>
#include<vector>
#include<string>
#include<stack>
#include<boost/lexical_cast.hpp>
std::map<std::string,double> varTable;
std::map<std::string,std::string> typeTab;
std::map<std::string,std::string> varStringTbl;
std::vector<VarInitExprAST*> args;
std::stack<std::vector<ExprAST*>* > parseStack;
std::vector<ExprAST*> topLevel;
std::vector<ExprAST*>* lines = &topLevel;
std::vector<ExprAST*> tmpLines;
std::vector<ExprAST*> callArgs;
extern int yylex(void);
extern void storeVar(char*,double);
extern void storeStringVar(char*,char*);
extern int lineNum;
extern map<string, Value*> NamedValues;
using namespace std;
void yyerror(char const *s) 
{ 
  cerr << "\033[31m ERROR: \033[37m" << s << " in line: " << lineNum << endl;
}
%}

%code requires
{
#include "tree.h"
}

%require "3.0"
%error-verbose

%union
{
  int intVal;
  char charVal;
  double doubleVal;
  const char* strVal;
  VarInitExprAST* varInit;
  TypeCastExprAST* tc;
  FunctionAST* fdef;
  KernelAST* kdef;
  PrototypeAST* proto;
  CallExprAST* functionCall;
  IfExprAST* _if;
  BinaryExprAST* bo;
  DoubleExprAST* dval;
  IntExprAST* ival;
  ExprAST* expr;
  globalVarExprAST* glob;
  stringExprAST* strAST;
  CharExprAST* charAST;
  UnaryExprAST* uo;
  ForExprAST* fLoop;
  ArrayIndexAST* arr;
  nullExprAST* null;
  std::vector<ExprAST*>* blk;
};

%token NUMBER FLOAT
%token STRING ID END CHARACTER
%token FUNC KERNEL EXTERN FOR IF ELSE ELIF IMPORT RETURN GLOBAL
%token INT DOUBLE CHAR STR
%token RARROW LARROW CAROT
%token EQUAL COMPEQUAL GT LT GTE LTE NEQUAL PLUS MINUS MUL DIV MOD EMARK QMARK AND OR LSBRACE RSBRACE LPAREN RPAREN LBRACE RBRACE AT DOT COMMA COLON SEMICOLON

%left EQUAL;
%left LT GT LTE GTE;
%left PLUS MINUS;
%left MUL DIV;
%nonassoc LPAREN

%type <intVal> NUMBER
%type <doubleVal> FLOAT
%type <strVal> INT
%type <strVal> DOUBLE
%type <strVal> CAROT
%type <strVal> AT
%type <strVal> CHAR
%type <strVal> STR 
%type <strVal> STRING
%type <strVal> SEMICOLON
%type <strVal> PLUS
%type <strVal> MINUS
%type <strVal> DIV
%type <strVal> MUL
%type <strVal> DOT
%type <strVal> EQUAL
%type <strVal> FUNC
%type <strVal> IMPORT 
%type <strVal> EXTERN
%type <strVal> GLOBAL
%type <strVal> KERNEL
%type <strVal> RARROW
%type <strVal> LARROW
%type <strVal> LBRACE
%type <strVal> LPAREN
%type <strVal> RPAREN
%type <strVal> RBRACE
%type <strVal> EMARK
%type <strVal> ID
%type <charVal> CHARACTER

%type <strVal> dataType 
%type <strVal> pointerType
%type <varInit> varDef
%type <proto> funcDef
%type <proto> kernelDef
%type <proto> funcProto
%type <proto> kernelProto
%type <functionCall> funcCall
%type <_if> ifBranch
%type <blk> elseBranch
%type <bo> binOp
%type <expr> numVal
%type <expr> funcParams
%type <charAST> chara
%type <uo> unaryOp
%type <proto> extern
%type <fLoop> forLoop
%type <blk> block
%type <blk> expressions
%type <blk> expression
%type <charVal> operators
%type <strVal> modImport
%type <arr> array
%type <glob> globalDef
%start program

%%
program: expressions

expressions: expressions expression 
           | expression { $$ = $1; }

expression: varDef  { lines->push_back($1); }
          | globalDef { lines->push_back($1); }
          | modImport { loadModule($1); lineNum=0; }
          | funcDef  { FunctionAST* tmp = new FunctionAST($1,*lines); lines = parseStack.top(); parseStack.pop(); lines->push_back(tmp); NamedValues.clear(); }
          | kernelDef { KernelAST* tmp = new KernelAST($1,*lines); lines = parseStack.top(); parseStack.pop(); lines->push_back(tmp); NamedValues.clear(); }
          | forLoop { lines->push_back($1); }
          | extern { lines->push_back($1); }
          | funcCall  { lines->push_back($1); }
          | ifBranch  { lines->push_back($1);}
          | binOp  { lines->push_back($1); }
          | chara { lines->push_back($1); }
          | unaryOp {lines->push_back($1); }
          | ID  { lines->push_back(new VariableExprAST($1,typeTab[$1]));}

modImport: IMPORT ID SEMICOLON { $$ = $2; }

funcParam: ID { callArgs.push_back(new VariableExprAST($1,typeTab[$1])); }
          | numVal { callArgs.push_back($1); }
          | binOp { callArgs.push_back($1); }
        
funcParams: funcParams COMMA funcParam
          | funcParam

chara: CHARACTER { $$ = new CharExprAST($1); }

funcCall: ID LPAREN funcParams RPAREN { $$ = new CallExprAST($1,callArgs); callArgs.clear(); }
        | ID LPAREN RPAREN { $$ = new CallExprAST($1,callArgs); }

extern: EXTERN funcProto SEMICOLON { $$ = $2; }

dataType: INT { $$ = "int"; }
        | DOUBLE { $$ = "double"; }
        | CHAR { $$ = "char"; }
        | STR { $$ = "string"; }
        | pointerType  { $$ = $1; }

pointerType: INT CAROT { $$ = "ints"; }
           | DOUBLE CAROT { $$ = "doubles"; }
           | CHAR CAROT { $$ = "chars"; }

varDef: dataType ID { $$ = new VarInitExprAST($2,$1,NULL); typeTab[$2] = $1; }
      | dataType ID EQUAL binOp { $$ = new VarInitExprAST($2,$1,$4); typeTab[$2] = $1; }
      | dataType ID EQUAL unaryOp { $$ = new VarInitExprAST($2,$1,$4); typeTab[$2] = $1; }
      | dataType LSBRACE numVal RSBRACE ID { string str($1); str += "Array"; typeTab[$5] = str; $$ = new VarInitExprAST($5,str.c_str(),NULL,$3);  }

globalDef: GLOBAL dataType ID SEMICOLON { cout << "GLOBAL" << endl; $$ = new globalVarExprAST($3,$2,NULL); typeTab[$3] = $2; }
         | GLOBAL dataType ID EQUAL binOp SEMICOLON { $$ = new globalVarExprAST($3,$2,$5); typeTab[$3] = $2; }
         | GLOBAL dataType ID EQUAL unaryOp SEMICOLON { $$ = new globalVarExprAST($3,$2,$5); typeTab[$3] = $2; }
         | GLOBAL dataType LSBRACE numVal RSBRACE ID SEMICOLON { string str($2); str += "Array"; typeTab[$6] = str; $$ = new globalVarExprAST($6,str.c_str(), NULL,$4); }

paramDefs: paramDefs COMMA paramDef
         | paramDef 

paramDef: dataType ID { VarInitExprAST* temp = new VarInitExprAST($2,$1,NULL); args.push_back(temp); }

openBlock: LBRACE {  parseStack.push(lines); lines = new vector<ExprAST*>(); } 

closeBlock: RBRACE  

block: openBlock expressions closeBlock 
     | openBlock closeBlock

forLoop: FOR ID EQUAL numVal COMMA numVal block { vector<ExprAST*> tmp = *lines; $$ = new ForExprAST($2,$4,$6,new IntExprAST(1),tmp); lines = parseStack.top(); parseStack.pop();  } 
       | FOR ID EQUAL numVal COMMA numVal COMMA numVal block  /*{ $$ = new ForExprAST($2,$4,$6,$8,$9); } */

elseBranch: ELSE block               { tmpLines = *lines; $$ = &tmpLines; lines = parseStack.top(); parseStack.pop(); }

ifBranch: IF binOp block             { vector<ExprAST*> tmp = *lines; $$ = new IfExprAST($2,tmp); lines = parseStack.top(); parseStack.pop(); }
        | IF unaryOp block           { vector<ExprAST*> tmp = *lines; $$ = new IfExprAST($2,tmp); lines = parseStack.top(); parseStack.pop(); }
        | IF binOp block elseBranch  { vector<ExprAST*> tmp = *lines; $$ = new IfExprAST($2,tmp,*$4); lines = parseStack.top(); parseStack.pop();}
        | IF unaryOp block elseBranch  { vector<ExprAST*> tmp = *lines; $$ = new IfExprAST($2,tmp,*$4); lines = parseStack.top(); parseStack.pop();}

funcDef: funcProto block { $$ = $1; }

kernelDef: kernelProto block { $$ = $1;}

kernelProto: KERNEL ID LPAREN paramDefs RPAREN LARROW NUMBER { $$ = new PrototypeAST($2,args,boost::lexical_cast<string>($7),true); args.clear(); }
           | KERNEL ID LPAREN           RPAREN LARROW NUMBER { $$ = new PrototypeAST($2,args,boost::lexical_cast<string>($6),true); args.clear(); }

funcProto: FUNC ID LPAREN paramDefs RPAREN RARROW dataType { $$ = new PrototypeAST($2,args,$7); args.clear();  }
         | FUNC ID LPAREN           RPAREN RARROW dataType { $$ = new PrototypeAST($2,args,$6); args.clear();  }

unaryOp: ID CAROT { $$ = new UnaryExprAST('^',new VariableExprAST($1,typeTab[$1])); }
       | operators array { $$ = new UnaryExprAST($1, $2); }
       | operators ID { $$ = new UnaryExprAST($1,new VariableExprAST($2,typeTab[$2])); }
       | EMARK binOp { $$ = new UnaryExprAST('!', $2); }
      

operators: CAROT { $$ = '^'; }
         | AT { $$ = '@'; }
         | MOD  { $$ = '%'; }
         | QMARK  { $$ = '?'; }
         | AND  { $$ = '&'; }
         | OR   { $$ = '|'; }
         | LSBRACE  { $$ = '['; }
         | RSBRACE  { $$ = ']'; }
         | DOT    { $$ = '.'; }
         | COLON  { $$ = ':'; }
         | SEMICOLON { $$ = ';'; }
         | MINUS { $$ = '-'; }

numVal: NUMBER { $$ = new IntExprAST($1); }
      | unaryOp { $$ = $1; }
      | MINUS NUMBER { int tmp = 0-$2; $$ = new IntExprAST(tmp); }  
      | FLOAT { $$ = new DoubleExprAST($1); }
      | MINUS FLOAT { double tmp = 0-$2; $$ = new DoubleExprAST(tmp); }
      | chara { $$ = $1; }
      | STRING { $$ = new stringExprAST($1,strlen($1)); }
      | ID { $$ = new VariableExprAST($1,typeTab[$1]); }
      | funcCall { $$ = $1; }
      | array { $$ = $1; }

array: ID LSBRACE binOp RSBRACE { $$ = new ArrayIndexAST($3,$1); }
     | ID LSBRACE RSBRACE { $$ = new ArrayIndexAST(new nullExprAST(), $1); }

binOp: numVal
     | LPAREN binOp RPAREN { $$ = $2; }
     | ID EQUAL binOp { $$ = new BinaryExprAST('=', new VariableExprAST($1,typeTab[$1]),$3); }
     | array EQUAL binOp { $$ = new BinaryExprAST('=', $1, $3); }
     /*| ID EQUAL unaryOp { $$ = new BinaryExprAST('=', new VariableExprAST($1,typeTab[$1]), $3); }*/
     | binOp PLUS binOp { $$ = new BinaryExprAST('+',$1,$3); }
     | binOp MINUS binOp { $$ = new BinaryExprAST('-',$1,$3); }
     | binOp DIV binOp { $$ = new BinaryExprAST('/',$1,$3); }
     | binOp MUL binOp { $$ = new BinaryExprAST('*',$1,$3); }
     | binOp LT numVal { $$ = new BinaryExprAST('<', $1, $3); }
     | binOp GT numVal { $$ = new BinaryExprAST('>', $1, $3); }
     | binOp LTE numVal { $$ = new BinaryExprAST('L', $1, $3); }
     | binOp GTE numVal { $$ = new BinaryExprAST('G', $1, $3); }
     | binOp NEQUAL numVal { $$ = new BinaryExprAST('N', $1, $3); }
     | binOp COMPEQUAL numVal { $$ = new BinaryExprAST('E', $1, $3); }
     | binOp operators binOp { $$ = new BinaryExprAST($2,$1,$3); }

